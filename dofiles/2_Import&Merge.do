****IMPORT & MERGE DOFILE*****
**Author: Caton Brewster, Dean Team RA
**Date created: 12/7/2015

/*
Purpose: This dofile imports the weekly CHCI sheet we receive with Access data and 
merges them with one another. Some formatting is done between merges to ensure compatability. 
The final dataset that is saved HAS PII AND MUST REMAIN ENCRYPTED. The weekly sheets
we receive are running lists of participants. 
*/

clear
set more off
set maxvar 32767	// Stata's max var limit
vers 12			//uncomment this if need to run for those using stata v12

tempfile access baseline dashboard noshows stickK
***************************************************
**1. Set Globals
***************************************************
c data
do dofiles/1_globals.do

***************************************************
**2. Import Data annd prep it 
***************************************************

**A. Access**********************************

import excel "$CHCIdta/Access/C2Q Patients $date.xlsx",  sheet("C2Q Patients") firstrow

//save raw version of sheets
saveold  "$rawdtaPII/CHCI/Access/RawAccess_$date", replace	
order StudyIDNumber ControlNumber

//prep data to merge
rename * a_*
rename a_StudyIDNumber StudyID
rename a_ControlNumber ChartNumber 

tostring ChartNumber StudyID, replace

saveold `access'

**B. Baseline Survey **********************************

import excel "$CHCIdta/BaselineSurvey/Data_all_$date.xls", sheet("Sheet1") firstrow clear

//save raw version of sheets
saveold  "$rawdtaPII/CHCI/Baseline/RawBaseline_$date", replace	

//prep data to merge
rename * b_*
rename b_StudyID StudyID
rename b_ChartNumber ChartNumber 

tostring ChartNumber StudyID, replace
drop if StudyID == "Open-Ended Response" 	//this is the row in the data that is auto-generated by SurveyMonkey

saveold `baseline'

/*
**C. Dashboard **********************************

import excel "$CHCIdta/Dashboard/$date.xls", sheet("Sheet1") firstrow clear

//save raw version of sheets
saveold  "$rawdtaPII/CHCI/Dashboard/RawDashboard_$date", replace	

saveold `dashboard'

**C. No Show Data **********************************

import excel "$CHCIdta/NoShows/$date.xls", sheet("Sheet1") firstrow clear

//save raw version of sheets
saveold  "$rawdtaPII/CHCI/NoShows/RawNoShows_$date", replace	

saveold `noshows'

**C. Endline**********************************

import excel "$CHCIdta/Endline/$date.xls", sheet("Sheet1") firstrow clear

//save raw version of sheets
saveold  "$rawdtaPII/CHCI/Endline/RawEndline_$date", replace	

saveold `endline'
*/

***************************************************
**3. Merge Data
***************************************************

use `access', clear

**Access with Baseline
merge 1:m StudyID ChartNumber using `baseline' 

assert _merge!= 2 		//_merge == 2 means we don't have access info. 
//br if _merge == 2	
drop if _merge == 2		//be sure to look at data first if assertion is false, but if we don't have anything in Access filled out, drop them. 

//assert _merge!= 1		//_merge == 1 means we don't have baseline data. 
//br if _merge == 1	
replace a_CommunicationNotes = a_CommunicationNotes + "Refused to do baseline. Dropped from program" if _merge == 1

**Check for inconsistencies in vars from both baseline and Access
//baseline has rows for email address and first/last name. We don't ask this. Drop them. 
drop b_EmailAddress b_FirstName b_LastName

preserve 
drop if _merge == 1 | _merge == 2 				//need to drop these temporarily for the logic checks in the lines below. 
assert StudyID == b_ReenterStudyID				//if these are false, look at what is going on. Will need to check in with CHC if these are false. 
assert ChartNumber == b_ReenterChartNumber
restore 

drop _merge		//[]evenutally create a dummy for when baseline wasn't filled out...? (12.24.15)

***************************************************
**3. Save raw data with PII 
***************************************************
saveold "$rawdtaPII/merged/1_reformatted/reformatted_raw_merged_$date", replace	
